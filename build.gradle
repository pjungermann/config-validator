/*
 * Copyright 2015 Patrick Jungermann
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
group 'com.github.pjungermann.config'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'findbugs'
apply plugin: 'pmd'
apply plugin: 'jacoco'
apply plugin: 'com.github.johnrengelman.shadow'

 //noinspection GroovyUnusedAssignment
sourceCompatibility = 1.8
//noinspection GroovyUnusedAssignment
targetCompatibility = 1.8

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.2'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // data policies
    compile 'com.intellij:annotations:12.0'

    // framework for dependency injection
    // (easier to plugin extensions)
    // + message bundle handling
    compile 'org.springframework:spring-context:4.1.7.RELEASE'

    testCompile 'junit:junit:4.12'
}

// ensure to build only acceptable versions
jacocoTestReport.dependsOn test
test.dependsOn findbugsTest, pmdTest
jar.dependsOn test, check, findbugsMain, pmdMain
shadowJar.dependsOn test, check, findbugsMain, pmdMain
build.dependsOn shadowJar

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

// report format settings
tasks.withType(FindBugs) {
    ignoreFailures = true

    reports {
        html.enabled = Boolean.parseBoolean(System.getProperty('gradle.reports.html', 'false'))
        xml.enabled = !html.isEnabled()
    }
}
