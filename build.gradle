/*
 * Copyright 2015 Patrick Jungermann
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
group 'com.github.pjungermann.config'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'findbugs'
apply plugin: 'pmd'
apply plugin: 'jacoco'
apply plugin: 'com.github.johnrengelman.shadow'

//noinspection GroovyUnusedAssignment
sourceCompatibility = 1.8
//noinspection GroovyUnusedAssignment
targetCompatibility = 1.8

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.2'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // logging
    compile 'org.slf4j:slf4j-log4j12:1.7.21'

    // data policies
    compile 'com.intellij:annotations:12.0'

    // dependency injection
    compile 'javax.inject:javax.inject:1'

    // framework for dependency injection
    // (easier to plugin extensions)
    // + message bundle handling
    compile 'org.springframework:spring-context:4.3.1.RELEASE'

    // config utils
    compile 'org.apache.commons:commons-lang3:3.4'

    // groovy config
    compile 'org.codehaus.groovy:groovy-all:2.4.7'
    // json config
    compile 'com.fasterxml.jackson.core:jackson-databind:2.8.0'
    // INI config
    compile 'org.ini4j:ini4j:0.5.4'
    // Yaml config
    compile 'org.yaml:snakeyaml:1.17'

    // config specification core
    compile 'commons-validator:commons-validator:1.4.1'

    testCompile 'junit:junit:4.12'

    // bean creation test to check compatibility with guice
    testCompile 'com.google.inject:guice:4.1.0'
    testCompile 'com.google.inject.extensions:guice-multibindings:4.0'
}

// ensure to build only acceptable versions
jacocoTestReport.dependsOn test
check.dependsOn findbugsMain, findbugsTest, pmdMain, pmdTest, jacocoTestReport
jar.dependsOn check
shadowJar.dependsOn check
build.dependsOn shadowJar

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

// report format settings
tasks.withType(FindBugs) {
    ignoreFailures = false

    reports {
        html.enabled = Boolean.parseBoolean(System.getProperty('gradle.reports.html', 'false'))
        xml.enabled = !html.isEnabled()
    }
}
